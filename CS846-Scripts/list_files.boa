####################################################
# list_files.boa
#
# Lists all the files in a Boa project. Used to populate
# the contribution network.
#
# Author: Bryan & Rafi
####################################################

# Starts by creating input and output variables.
currProject: Project = input;

# Files
bugs: output sum[string] of int;
commits: output sum[string] of int;

# Use the visitor pattern to visit each of the projects.
visit(currProject, visitor { 
	# Checks if the project matches the set ID.
	before proj: Project -> {
		if (proj.id != "<PLACEHOLDER>") stop;
	}
	
	# Goes through the revisions.
	before rev: Revision -> {
		rFlag : bool = isfixingrevision(rev);
		
		# From this goes through all the files.
		visit(rev, visitor {
			before file: ChangedFile -> {
				# Sets the number of bugs.
				if (rFlag){
					bugs[file.name] << 1;
				}
				
				# Sets the number of commits
				commits[file.name] << 1;
			}
		});
	}
});